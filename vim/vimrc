" Vimrc
" Mathijs Saey

set nocompatible                                    " Discard vi compatibility

" ------- "
" Plugins "
" ------- "

call plug#begin('~/.vim/bundle')                                               " Start plugin list

" Plugin list
" -----------

" Visuals
Plug 'tomasr/molokai'                                                          " Color scheme
Plug 'junegunn/goyo.vim'                                                       " Distraction free mode
Plug 'mhinz/vim-startify'                                                      " Custom start message
Plug 'junegunn/limelight.vim'                                                  " Only color current paragraph
Plug 'vim-airline/vim-airline'                                                 " Fancy status bar

" Git integration
Plug 'tpope/vim-fugitive'                                                      " Git integration
Plug 'airblade/vim-gitgutter'                                                  " Show diff in sidebar

" Tools
Plug 'w0rp/ale'                                                                " Asynchronous linter
Plug 'xolox/vim-misc'                                                          " General scripts, required for vim-session
Plug 'mbbill/undotree'                                                         " Visual undo tree
Plug 'mileszs/ack.vim'                                                         " Better file search
Plug 'godlygeek/tabular'                                                       " Easily align stuff
Plug 'xolox/vim-session'                                                       " Session Management
Plug 'pbrisbin/vim-mkdir'                                                      " Automatically create directories
Plug 'tpope/vim-surround'                                                      " Easily change surrounding braces, quotes, ...
Plug 'vim-scripts/Tagbar'                                                      " File minimap
Plug 'vim-scripts/utl.vim'                                                     " Universal text linking (required for vim-orgmode)
Plug 'chrisbra/improvedft'                                                     " Makes f and t work on multiple lines
Plug 'tpope/vim-commentary'                                                    " Easily turn lines into comments
Plug 'kshenoy/vim-signature'                                                   " Show marks in the sidebar
Plug 'tpope/vim-speeddating'                                                   " Date tools for vim, required for vim-orgmode
Plug 'rbgrouleff/bclose.vim'                                                   " Close buffer without changing layout
Plug 'vim-scripts/TaskList.vim'                                                " List uncompleted todo's
Plug 'terryma/vim-expand-region'                                               " Easily select regions
Plug 'vim-scripts/DeleteTrailingWhitespace'                                    " Allow the automatic deletion of trailing whitespace

" Languages
Plug 'zah/nim.vim', { 'for': 'nim' }                                           " Nim support
Plug 'lervag/vimtex', { 'for': 'tex' }                                         " Latex support
Plug 'jceb/vim-orgmode', {'for': 'org'}                                        " Org mode for vim
Plug 'derekwyatt/vim-scala', {'for': 'scala' }                                 " Scala support
Plug 'elixir-lang/vim-elixir', {'for': 'elixir'}                               " Syntax support for Elixir
Plug 'slashmili/alchemist.vim', {'for': 'elixir'}                              " IDE like tools for Elixir
Plug 'wlangstroth/vim-racket', { 'for': 'racket' }                             " Racket support
Plug 'gabrielelana/vim-markdown', { 'for': 'markdown'}                         " Improved markdown support
Plug 'octol/vim-cpp-enhanced-highlight', { 'for': 'cpp' }                      " Better CPP syntax highlighting
Plug 'hdima/python-syntax', { 'for': 'python' }                                " Better syntax highlighting for python
Plug 'vim-scripts/indentpython.vim', { 'for': 'python' }                       " Better python indentation

" Plugins with advanced setup

" Function to install the YCM server after updates.
function! BuildYCM(info)
  if a:info.status == 'installed' ||
   \ a:info.status == 'updated'   ||
   \ a:info.force
    !./install.py --clang-completer
  endif
endfunction

Plug 'Valloric/YouCompleteMe', { 'do': function('BuildYCM') }                  " Tab completion

call plug#end()                                                                " End of plugin list

" ----------- "
" Look & Feel "
" ----------- "

" Line numbers
set ruler                                           " Show line and column number
set number                                          " Draw line numbers
set cursorline                                      " Highlight current line
set colorcolumn=80                                  " Show a line on column 80

" Scroll bars
set guioptions-=r                                   " Hide right scrollbar
set guioptions-=l                                   " Hide left scrollbar
set guioptions-=L                                   " Hide left scrollbar when multiple panels exist

" Color Scheme
set t_Co=256                                        " Enable 256 color support
syntax enable                                       " Enable syntax highlighting
colorscheme molokai                                 " Set color scheme

" Font
set guifont=Menlo:h12                               " Set text font

" Status Bar + Airline
set laststatus=2                                    " Always show the status bar
set guifont=Meslo\ LG\ L\ Regular\ for\ Powerline   " Custom powerline font

" Buffer list
let g:airline_powerline_fonts=1                     " Populate airline font symbol table
let g:airline#extensions#tabline#enabled=1          " Show open buffers
let g:airline#extensions#tabline#buffer_min_count=2 " Only show tab line when at least two buffers are open
let g:airline#extensions#tabline#left_sep = ' '     " No left separator
let g:airline#extensions#tabline#left_alt_sep = '|' " Use | as the right separator for 'straight tabs

" Error symbols
let g:airline#extensions#ale#error_symbol = '⨉ '
let g:airline#extensions#ale#warning_symbol = '⚠ '

" Temporary Files
set backupdir=~/.tmp/vim/backup//                   " Directory for file backups
set directory=~/.tmp/vim/swap//                     " Directory for swap files
set undodir=~/.tmp/vim/undo//                       " Directory for undo files

" Sessions
let g:session_directory='~/.tmp/vim/sessions/'
let g:session_autosave='yes'                        " Always save sessions when quitting vim
let g:session_autoload = 'no'                       " Don't prompt for opening old sessions
let g:session_autosave_periodic=1                   " Save session every minute

" Startify
let g:startify_session_dir = g:session_directory    " Tell startify where to look for sessions
let g:startify_files_number = 5                     " Limit the amount of recently used files/dirs startify shows
let g:startify_custom_header =
  \ map(split(system(
  \ 'fortune -a |
  \ cowsay -f stegosaurus -W 70'
  \ ), '\n'), '"   ". v:val')
let g:startify_list_order = [
  \ ['Sessions:'],
  \ 'sessions',
  \ ['Recently opened files:'],
  \ 'files',
  \ ['Recently opened files in this directory:'],
  \ 'dir'
  \ ]

" Misc
set title                                           " Let vim set the window title
set ttyfast                                         " Indicates the terminal is fast and more chars can be sent
set undofile                                        " Save undo tree
set wildmenu                                        " Shows the available autocompletes in command menu
set autowrite                                       " Automatically save before compiling and some other actions
set lazyredraw                                      " Don't always redraw the screen
set backspace=indent,eol,start                      " Allow backspaces to remove more

let g:DeleteTrailingWhitespace_Action = 'delete'    " Remove trailing whitespace on save

" -------------- "
" Tabs & Spacing "
" -------------- "

set tabstop=2                                       " Draw tabs as 2 spaces
set shiftwidth=2                                    " Use 2 spaces as automatic indentation
set softtabstop=2                                   " Insert and remove tabs as 2 spaces
set autoindent                                      " Automatically copy amount of leading tabs from previous lines
set smartindent                                     " Add extra tabs in some cases
set expandtab                                       " Expand tabs to spaces

set list                                            " Visualize tabs, trailing spaces and various elements
set listchars=tab:--                                " - to mark tabs
set listchars+=trail:~                              " ~ to mark trailing spaces
set listchars+=extends:>                            " > to mark that a line continues beyond the edge of the screen
set listchars+=precedes:<                           " < to mark that a line starts beyond the left edge of the screen
set listchars+=conceal:*                            " * to mark hidden characters
set listchars+=nbsp:.                               " . to mark non-breakable spaces

set nowrap                                          " Don't auto-insert newlines when exceeding 80 characters
set sidescroll=1                                    " Scroll per charachter when sidescrolling
set wrapmargin=0                                    " Don't wrap based on screen edge

" --------------- "
" Search Settings "
" --------------- "

set incsearch                                       " Highlight characters as they are matched
set hlsearch                                        " Highlight matches
set showmatch                                       " Highlight matching [{()}]

set ignorecase                                      " Don't match case
set smartcase                                       " Unless the search term contains uppercase

let g:ackprg = 'ag --vimgrep'                       " Setup ag for ack.vim

" ------------------------ "
" Global Language Settings "
" ------------------------ "

" Settings for languages that should be loaded before the relevant
" fptplugin/after file is loaded

let g:tex_flavor = 'latex'                          " Always use latex

" ------------ "
" Autocommands "
" ------------ "

" Close the preview window if it's open when leaving insert mode.
" Refresh airline, to make sure it reflects the selected buffer.
" Use `InsertLeave` instead of `CompleteDone` to ensure that the
" documentation is still available while entering arguments.
autocmd InsertLeave *
  \ if pumvisible() == 0 |
  \   pclose | :AirlineRefresh |
  \ endif

" Add .nims as a nim file
autocmd BufNewFile,BufRead *.nims set filetype=nim

" Turn on Limelight when using Goyo
autocmd! User GoyoEnter Limelight
autocmd! User GoyoLeave Limelight!

" ------------------- "
" ConqueTerm Settings "
" ------------------- "

let g:ConqueTerm_ReadUnfocused = 1                  " Keep on refreshing when buffer is not active
let g:ConqueTerm_StartMessages = 0                  " Show warnings on startup when necessary
let g:ConqueTerm_InsertOnEnter = 0                  " Don't enter insert mode on start
let g:ConqueTerm_TERM = 'xterm-256color'            " Report color support for prettier output

let g:Conque_syntax_assoc = {}                      " Language to command mapping for syntax highlighting
let g:Conque_syntax_assoc.python3 = 'python'        " Only required when the command and the syntax names differ.
let g:Conque_syntax_assoc.ghci    = 'haskell'
let g:Conque_syntax_assoc.irb     = 'ruby'

function! Conque_set_syntax(term)
    if has_key(g:Conque_syntax_assoc, a:term.program_name)
        execute 'setlocal syntax=' . g:Conque_syntax_assoc[a:term.program_name]
    else
        execute 'setlocal syntax=' . a:term.program_name
    endif
endfunction

" call conque_term#register_function('after_startup', 'Conque_set_syntax')

" --------------------------- "
" Custom Functions & Commands "
" --------------------------- "

" Toggle relative and absolute line numbers
function! NumberToggle()
  if(&relativenumber == 1)
    set norelativenumber
  else
    set relativenumber
  endif
endfunc

" Toggle spellcheck
function! SpellToggle()
  set spell!
endfunc

" Creates a fancy comment box given a string
function! CommentBox(msg)
  let com = substitute(&commentstring, '%s', '', '')
  let com = substitute(l:com, ' ', '', '')
  let len = strlen(a:msg)

  let sur  = l:com . ' ' . repeat('-', l:len) . ' ' . l:com
  let mid  = l:com . ' ' . a:msg . ' ' . l:com

  call append('.', sur)
  call append('.', mid)
  call append('.', sur)
endfunction

" Creates a fancy comment line given a string
function! CommentLine(msg)
  let com = substitute(&commentstring, '%s', '', '')
  let com = substitute(l:com, ' ', '', '')
  let len = strlen(a:msg)

  let sur  = l:com . ' ' . repeat('-', l:len)
  let mid  = l:com . ' ' . a:msg

  call append('.', sur)
  call append('.', mid)
endfunction

" Get the currently selected text
" Source: http://stackoverflow.com/a/6271254/3329884
function! GetVisualSelection()
  let [lnum1, col1] = getpos("'<")[1:2]
  let [lnum2, col2] = getpos("'>")[1:2]
  let lines = getline(lnum1, lnum2)
  let lines[-1] = lines[-1][: col2 - (&selection == 'inclusive' ? 1 : 2)]
  let lines[0] = lines[0][col1 - 1:]
  return join(lines, "\n")
endfunction

" Highlight a piece of text and add it to the system clipboard
" Args: txt, [theme, fontSize, fileType, font]
"   txt: The text to be highlighted
"   theme: The highlight theme to use (defaults to seashell)
"   fontSize: The font size to use (defaults to 12)
"   fileType: The language of txt (defaults to the current filetype)
"   font: The font to use (defaults to menlo)
function! Highlight(txt, ...) range
  let txt      = shellescape(a:txt)
  let theme    = (a:0 >= 1) ? a:1 : 'seashell'
  let fontSize = (a:0 >= 2) ? a:2 : 12
  let fileType = (a:0 >= 3) ? a:3 : &filetype
  let font     = (a:0 >= 4) ? a:4 : 'menlo'
  let cmd =
    \ '!' . ' ' . 'echo' . ' ' . txt . ' | ' .
    \ 'highlight --out-format=rtf' . ' ' .
    \ '--style=' . theme . ' ' .
    \ '--font-size=' . fontSize . ' ' .
    \ '--font=' . font . ' ' .
    \ '--syntax=' . fileType . ' ' .
    \ '--no-trailing-nl' . ' ' .
    \ '|' . ' ' .  'pbcopy'
  silent exec cmd
endfunction

command! -nargs=*          CommentBox  call CommentBox(<q-args>)
command! -nargs=*          CommentLine call CommentLine(<q-args>)
command! -nargs=* -range   Highlight   call Highlight(GetVisualSelection(), <f-args>)

" ------------------- "
" Custom Key Bindings "
" ------------------- "

" Use space as leader
let mapleader = "\<Space>"
" Use backslash as local leader
let maplocalleader = "\\"

" System copy/paste
nnoremap <Leader>y "+y
nnoremap <Leader>d "+d
nnoremap <Leader>p "+p
nnoremap <Leader>P "+P
vnoremap <Leader>y "+y
vnoremap <Leader>d "+d
vnoremap <Leader>p "+p
vnoremap <Leader>P "+P
" Close window
nnoremap  <Leader>qa :qall<CR>
" Close buffer
nnoremap  <Leader>qw :q<CR>
" Close buffer but preseve layout
nnoremap  <Leader>qb :Bclose<CR>
" Easier split changing
nnoremap <Leader>m <C-W>
" Write file
nnoremap <Leader>w :write<CR>
" Turn off search highlighting
nnoremap <Leader>n :nohlsearch<CR>
" Show the visual undo tree
nnoremap <Leader>u :UndotreeToggle<CR>
" Tabularize =
nnoremap <Leader>= :Tabularize /=<CR>
" Toggle line numbers
nnoremap <Leader>l :call NumberToggle()<CR>
" Highligh selected text
vnoremap <Leader>h :Highlight<CR>
" Enter distraction free mode (or leave it)
nnoremap <leader>g :Goyo<CR>

" Spelling
nnoremap <Leader>so  :call SpellToggle()<CR>
nnoremap <Leader>sn  :setlocal spelllang=nl<CR>
nnoremap <Leader>seb :setlocal spelllang=en_gb<CR>
nnoremap <Leader>sea :setlocal spelllang=en_us<CR>

" Disable arrow keys in normal mode
noremap <up>    : echoe "use k"<CR>
noremap <down>  : echoe "use j"<CR>
noremap <left>  : echoe "use h"<CR>
noremap <right> : echoe "use l"<CR>

" Easily get out of insert mode
inoremap fj <Esc>
inoremap jf <Esc>

" Use <Leader>; as a shortcut to : to avoid pressing shift
noremap <Leader><Leader> :
